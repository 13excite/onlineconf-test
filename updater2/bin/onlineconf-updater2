#!/usr/bin/perl

use strict;
use warnings;
use AnyEvent;
use EV;
use Getopt::Long;
use Log::Dispatch;
use POSIX qw/strftime/;
use YAML;
use MR::OnlineConf2::Updater;

my %options = (
    config => '/usr/local/etc/onlineconf2.yaml',
);

GetOptions(\%options, qw/
    config|c=s
    loglevel|l=s
/);

my $config = YAML::LoadFile($options{config});

my $log = Log::Dispatch->new(
    outputs => [[ 'Screen', min_level => $options{loglevel} || $config->{loglevel} || 'info', newline => 1 ]],
    callbacks => sub {
        my %in = @_;
        my $prefix = sprintf "%s [%d] %s: ", strftime("%F %T", localtime()), $$, $in{level};
        my $message = $in{message};
        $message =~ s/^/$prefix/gm;
        $message =~ s/\n$//;
        return $message;
    },
);

$log->info("onlineconf-updater2 started\n");

my $updater = MR::OnlineConf2::Updater->new(
    config => $config,
    log    => $log,
);
$updater->run();

$log->info("onlineconf-updater2 finished\n");
